#=: gt_lt
$$- XML-Code im Zwischenspeicher wird fuer <code>-Container aufbereitet
$$-          (z.B. impress.js)
$$-          Spitzklammern werden aufgeloest:
$$-            <tag> -- &lt;tag&gt;
$$-          entfernt werden:
$$-            #[0009]
$$-            blanks > 1
$$!
$$ MODE TUSCRIPT,{}
-- Zwischenspeicher import fuer Linux und MAC einrichten
system=SYSTEM()

IF (system=="MAC") THEN
 EXECUTE #*pb,import
ELSEIF (system=="LINUX") THEN
 EXECUTE #*cb,import
ENDIF

-- Zwischenspeicher auf Variable xml einlesen
FETCH/CLIPBOARD xml

-- Austauschtabelle - Separator: ~
MODE DATA
$$ SET/DATA gtlt
$$- Spitzklammern
~<~&lt;~
~>~&gt;~
$$- Tabulator
~#\[0009\]~  ~
$$- Leerzeichen
~{2-99} ~ ~
$$ END DATA
$$ MODE TUSCRIPT,{}
BUILD X_TABLE gtlt = $gtlt
xml=EXCHANGE (xml,gtlt)

-- Inhalt der Variable xml in Zwischenspeicher ausgeben
DEFINE/CLIPBOARD xml

-- Zwischenspeicher export fuer Linux und MAC einrichten
SELECT system
 CASE "MAC"
  EXECUTE #*pb,export
 CASE "LINUX"
  EXECUTE #*cb,export
ENDSELECT
#=: INIT
#de,tustep.ini
#-- Protokoll ein und Inhalt der Makrodatei anzeigen
#-- Aktivieren fuer Testzwecke
#-pr,+
#-in,$
#$gt_lt
#-- Programm beenden
#-- Deaktivieren fuer Testzwecke
#no,+
